//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//============================================================

prontera,170,180,1	script	Estilista Personalizado	122,{
	setarray .@Estilos[1],
		getbattleflag("max_cloth_color"),
		getbattleflag("max_hair_style"),
		getbattleflag("max_hair_color");
	setarray .@Visual[1],
		LOOK_CLOTHES_COLOR,
		LOOK_HAIR,
		LOOK_HAIR_COLOR;
	set .@s, select(" ~ Cor das roupas: ~ Penteado: ~ Cor do cabelo");
	set .@Reverter, getlook(.@Visual[.@s]);
	set .@Estilo,1;
	while(1) {
		setlook .@Visual[.@s], .@Estilo;
		message strcharinfo(0),"Este é o estilo #"+.@Estilo+".";
		set .@menu$, " ~ Próximo (^0055FF"+((.@Estilo!=.@Estilos[.@s])?.@Estilo+1:1)+"^000000): ~ Anterior (^0055FF"+((.@Estilo!=1)?.@Estilo-1:.@Estilos[.@s])+"^000000): ~ Ir para...: ~ Reverter para original (^0055FF"+.@Reverter+"^000000)";
		switch(select(.@menu$)) {
		case 1:
			set .@Estilo, ((.@Estilo != .@Estilos[.@s]) ? .@Estilo+1 : 1);
			break;
		case 2:
			set .@Estilo, ((.@Estilo != 1) ? .@Estilo-1 : .@Estilos[.@s]);
			break;
		case 3:
			message strcharinfo(0),"Escolha um estilo entre 1 - "+.@Estilos[.@s]+".";
			input .@Estilo,0,.@Estilos[.@s];
			if (!.@Estilo)
				set .@Estilo, rand(1,.@Estilos[.@s]);
			break;
		case 4:
			set .@Estilo, .@Reverter;
			setlook .@Visual[.@s], .@Reverter;
			break;
		}
	}
}
