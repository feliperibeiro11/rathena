//===== rAthena Script =======================================
//= Custom Halloween Event 2006
//===== By: ==================================================
//= Stryker
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Halloween Event Script
//===== Additional Comments: =================================
//= 1.1 - Added trick to script
//= 1.2 fixed bugs, optimized. Now uses 1 var instead of 3 [Lupus]
//============================================================

prontera,151,190,5	script	Evento Halloween	807,{
	mes "[Evento Halloween]";
	if (Class == Job_Novice) {
		mes "Desculpe, mas você precisa ter uma classe para participar do evento.";
		emotion ET_SWEAT, playerattached();
		close;
	}
	if ((EVENT_HALWN06&1) && (EVENT_HALWN06&2)) {
		mes "Desculpe, mas você já completou o evento.";
		emotion ET_SORRY;
		close;
	}
	mes "Saudações, humano animado!";
	next;
	mes "[Evento Halloween]";
	mes "O Halloween está chegando, e está quase na hora de se aventurar em lugares que poucos ousam ir.";
	next;
	mes "[Evento Halloween]";
	mes "Gostosuras ou travessuras, qual você prefere?";
	next;
	menu "Travessura",-, "Gostosura",L_Gostosura;

	mes "[Evento Halloween]";
	if (EVENT_HALWN06&2) {
		mes "Desculpe, mas você já teve uma travessura. Que tal escolher uma gostosura agora?";
		emotion ET_SORRY;
		close;
	}
	mes "Dê-me um momento...";
	next;
	set BaseExp, BaseExp + BaseLevel*1000;
	set EVENT_HALWN06, EVENT_HALWN06|2;

	mes "[Evento Halloween]";
	mes "Aqui está sua gostosura. Sua Base EXP foi levemente aumentada.";
	next;
	mes "[Evento Halloween]";
	mes "Espero que aproveite sua travessura e tenha um Feliz Halloween!";
	close;

L_Gostosura:
	mes "[Evento Halloween]";
	if (EVENT_HALWN06&1) {
		mes "Desculpe, mas você já teve uma gostosura. Que tal escolher uma travessura agora?";
		emotion ET_SORRY;
		close;
	}
	if (!(EVENT_HALWN06&4)) {
		mes "Ótimo! Isso será muito divertido.";
		next;
		mes "[Evento Halloween]";
		mes "Vou enviar você para uma caça aos itens.";
		next;
		mes "[Evento Halloween]";
		mes "Traga de volta todos os itens necessários, e eu vou criar uma gostosura para você.";
		next;
		mes "[Evento Halloween]";
		mes "Não é apenas uma gostosura normal que você pode comer, mas algo que você pode usar na cabeça.";
		next;
		mes "[Evento Halloween]";
		mes "Mas, como tudo que é feito, alguns itens são necessários primeiro.";
		mes "A lista:";
		mes "1 Chapéu,";
		mes "30 Abóboras, e";
		mes "20 Pedaços de Pano Preto.";
		next;
		mes "[Evento Halloween]";
		mes "Se apresse agora.";
		set EVENT_HALWN06, EVENT_HALWN06|4;
		close;
	}
	mes "Você conseguiu os itens que pedi ou precisa ver a lista novamente?";
	next;
	menu "Consegui",-, "Não Tenho",L_Nao_Tenho, "Ver Lista",L_Lista;

	mes "[Evento Halloween]";
	if ((countitem(2220) < 1) || (countitem(535) < 30) || (countitem(7205) < 20)) {
		mes "Você não tem itens suficientes. Volte quando tiver.";
		emotion ET_SORRY;
		close;
	}
	delitem 2220,1;
	delitem 535,30;
	delitem 7205,20;
	getitem 5134,1;
	set EVENT_HALWN06, EVENT_HALWN06|1;
	mes "Bom trabalho! Espero que fique feliz com seu item.";
	emotion ET_CONGRATULATION;
	next;
	mes "[Evento Halloween]";
	mes "Aproveite o resto do seu Halloween.";
	emotion ET_SCISSOR, playerattached();
	close;

L_Nao_Tenho:
	mes "[Evento Halloween]";
	mes "Volte e procure por eles. Gostaria de ver a lista?";
	next;
	menu "Não",-, "Sim",L_Lista;
	close;

L_Lista:
	mes "[Evento Halloween]";
	mes "Eu preciso dos seguintes itens:";
	mes "1 Chapéu,";
	mes "30 Abóboras, e";
	mes "20 Pedaços de Pano Preto.";
	close;
}
