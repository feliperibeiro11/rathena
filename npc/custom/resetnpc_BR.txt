//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
prontera,148,195,4	script	Garota Resetadora	646,{
	//	 		Habilidades,	Estatísticas,	Ambos,	Limite
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[Garota Resetadora]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Desculpe, você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+" na sua vida.";
		close;
	}
	mes "Eu sou a Garota Resetadora.";
	mes "Resetar Estatísticas: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "Resetar Habilidades: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "Resetar Ambos: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "Você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+", então use "+((.@Reset[3]>1)?"elas":"isso")+" com sabedoria.";
	mes "Por favor, selecione o serviço desejado:";
	next;
	set .@i,(select("^FF3355Resetar Habilidades:Resetar Estatísticas:Resetar Ambos^000000:Cancelar"));
	if(.@i > 3) close;
	mes "[Garota Resetadora]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Desculpe, você não possui Zeny suficiente.";
		close;
	}
	if(.@Reset[3]){
		mes "Você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+" na sua vida, tem certeza?";
		if(select("Deixe-me pensar:Está bem") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "Pronto, está feito!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
